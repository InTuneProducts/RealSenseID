apply plugin: 'com.android.library'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 27

        externalNativeBuild.cmake {
            arguments += ["-DRSID_C_CLIENT=ON", "-DENABLE_TESTING=OFF", "-DRSID_DEBUG_MODE=ON", "-DRSID_PREVIEW=ON"]
            cppFlags += ["-frtti", "-fexceptions", "-fvisibility=hidden", "-std=c++14"]
        }

        ndk.abiFilters = ['arm64-v8a', 'x86_64']
        versionName "2"
    }

    externalNativeBuild {
        cmake {
            path = "../../CMakeLists.txt"
            version "3.10.0+"
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            resValue "string", "app_version", "${defaultConfig.versionName}"
        }

        release {
            minifyEnabled false
            resValue "string", "app_version", "${defaultConfig.versionName}"
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            def libName = "RealSenseID"
            def buildType = variant.buildType.name
            def newName = "${libName}_${buildType}_${defaultConfig.versionName}.aar"

            outputFileName = newName
        }
    }
}

project.afterEvaluate {
    // but it allows CMake/SWIG to run before Android Studio complains about missing generated files
    javaPreCompileDebug.dependsOn externalNativeBuildDebug
    javaPreCompileRelease.dependsOn externalNativeBuildRelease
}